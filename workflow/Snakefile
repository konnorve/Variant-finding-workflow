# The main entry point of your workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.
from pathlib import Path
import pandas as pd

configfile: "config/config.yaml"

SAMPLE_TABLE = pd.read_csv(config["samples"], index_col="sample_name", sep="\t")
SAMPLE_TABLE.index = SAMPLE_TABLE.index.map(str)
ALL_SAMPLES = SAMPLE_TABLE.index.to_list()
PACBIO_SAMPLES = SAMPLE_TABLE[SAMPLE_TABLE['ccs_read'].notnull()].index.to_list() if 'ccs_read' in SAMPLE_TABLE.columns else []
ILLUMINA_SAMPLES = SAMPLE_TABLE[SAMPLE_TABLE['fwd_read'].notnull() & SAMPLE_TABLE['rev_read'].notnull()].index.to_list() if 'fwd_read' in SAMPLE_TABLE.columns and 'rev_read' in SAMPLE_TABLE.columns else []

scratch_dir = Path(config["scratch_dir"])
results_dir = Path(config["results_dir"])
logging_dir = Path(config["logging_dir"])
scratch_dict = {
    "trimmed_reads": scratch_dir / "1_trimmed_reads", 
    "genome_index": scratch_dir / "2_genome_index",
    "mapped_reads": scratch_dir / "3_mapped_reads",
    "deduped": {
        "bams": scratch_dir / "4_PCR_duplicates_removed" / "bams",
        "metadata": scratch_dir / "4_PCR_duplicates_removed" / "metadata",
        "depth": scratch_dir / "4_PCR_duplicates_removed" / "read_depth",
    },
    "vcfs": scratch_dir / "5_calling_variants",
    "assembly":{
        "spades": scratch_dir / "6_de_novo_assembly" / "spades",
        "flye": scratch_dir / "6_de_novo_assembly" / "flye",
        "ragtag": scratch_dir / "6_de_novo_assembly" / "ragtag"
    },
    "done_files": {
        "bwa_index": scratch_dir / "done_files" / "bwa_index.done",
        "bowtie2_index": scratch_dir / "done_files" / "bowtie2_index.done",
        "hisat_index": scratch_dir / "done_files" / "hisat_index.done",
        "analysis": scratch_dir / "done_files" / "analysis"
    },
}

results_dict = {
    "raw_data" : {
        "genome_dup_stats": results_dir / 'raw_data' / 'genome_dup_stats',
        "raw_vcfs": results_dir / 'raw_data' / 'vcfs',
        "mummer" : results_dir / 'raw_data' / 'mummer',
    },
    "vcf_compare" : results_dir / "vcf_comparisons",
    "phased_gene_variants" : results_dir / "phased_gene_variants",
    "lineage_mutation_occurance" : results_dir / "lineage_mutation_occurance",
    "figures" : {
        "depth_histogram" : results_dir / "figures" / "depth_histogram",
        "depth_genome" : results_dir / "figures" / "depth_genome",
        "depth_genome_zscore" : results_dir / "figures" / "depth_genome_zscore",
        "coverage_gc" : results_dir / "figures" / "coverage_gc",
        "dotplot" : results_dir / "figures" / "dotplot",
        "genome_viewer" : results_dir / "figures" / "genome_viewer",
    }
}

def mk_out(group, rule, wildcards=None):
    if wildcards:
        p = logging_dir / group / rule / f"{'.'.join(wildcards).replace(' ', '.')}.out"
    else:
        p = logging_dir / group / f"{rule}.out"
    p.parent.mkdir(parents=True, exist_ok=True)
    return str(p)

def mk_err(group, rule, wildcards=None):
    if wildcards:
        p = logging_dir / group / rule / f"{'.'.join(wildcards).replace(' ', '.')}.err"
    else:
        p = logging_dir / group / f"{rule}.err"
    p.parent.mkdir(parents=True, exist_ok=True)
    return str(p)

rule all:
    input:
        results_dict["lineage_mutation_occurance"] / "lineage_gene_mutation_heatmap.png",
        directory(results_dict["vcf_compare"]),
        expand(results_dict["figures"]["depth_histogram"] / "{sample}_depth_histogram.png", sample=ALL_SAMPLES),
        expand(results_dict["figures"]["depth_genome"] / "{sample}_depth_genome.png", sample=ALL_SAMPLES),
        expand(results_dict["figures"]["depth_genome_zscore"] / "{sample}_depth_genome_zscore.png", sample=ALL_SAMPLES),
        expand(results_dict["figures"]["coverage_gc"] / "{sample}_coverage_gc.png", sample=ALL_SAMPLES),
        expand(results_dict["figures"]["dotplot"] / "{sample}_dotplot.png", sample=ALL_SAMPLES),

include: "rules/mapping.smk"


include: "rules/run_trim.smk"


include: "rules/samtools.smk"


include: "rules/remove_PCR_duplicates.smk"


include: "rules/call_variants.smk"


include: "rules/post_analysis.smk"


include: "rules/align_genomes.smk"


include: "rules/assembly.smk"
