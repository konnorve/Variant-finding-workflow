# The main entry point of your workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.
from pathlib import Path
import pandas as pd

configfile: "config/config.yaml"

SAMPLE_TABLE = pd.read_csv(config["samples"], index_col="sample_name", sep="\t")
SAMPLE_TABLE.index = SAMPLE_TABLE.index.map(str)
SAMPLES = SAMPLE_TABLE.index.values

scratch_dir = Path(config["scratch_dir"])
results_dir = Path(config["results"])
scratch_dict = {
    "trimmed_reads": scratch_dir / "1_trimmed_reads", 
    "genome_index": scratch_dir / "2_genome_index",
    "mapped_reads": scratch_dir / "3_mapped_reads",
    "deduped": {
        "bams": scratch_dir / "4_PCR_duplicates_removed" / "bams",
        "metadata": scratch_dir / "4_PCR_duplicates_removed" / "metadata",
        "depth": scratch_dir / "4_PCR_duplicates_removed" / "read_depth",
    },
    "vcfs": scratch_dir / "5_variant_calls",
    "all_calls": {
        "raw": scratch_dir / "6_all_calls" / "raw_vcf",
        "calls_only": scratch_dir / "6_all_calls" / "vcf_calls_only",
        "header": scratch_dir / "6_all_calls" / "vcf_headers",
    },
    "assembly":{
        "spades": scratch_dir / "7_de_novo_assembly" / "spades",
        "flye": scratch_dir / "7_de_novo_assembly" / "flye",
        "ragtag": scratch_dir / "7_de_novo_assembly" / "ragtag"
    },
    "done_files": {
        "index": scratch_dir / "done_files" / "index.done",
        "analysis": scratch_dir / "done_files" / "analysis"
    },
    "pairwise_alignment": scratch_dir / "8_parwise_alignment",
    
}

results_dict = {
    "all_variants_df" : results_dir / "variant_dfs",
    "annotated_vcfs" : results_dir / "annotated_vcfs",
    "depth_histograms" : results_dir / "depth_histograms",
    "depth_genome" : results_dir / "depth_genome",
    "depth_genome_zscore" : results_dir / "depth_genome_zscore",
    "genome_bin_stats" : results_dir / "genome_bin_stats",
    "coverage_gc_correlation" : results_dir / "coverage_gc_correlation",
    "dotplots" : results_dir / "dotplots",
    "distance_matix" : results_dir / "distance_matrix",
    "distance_clustering" : results_dir / "distance_clustering",
}


rule all:
    input:
        expand(scratch_dict["done_files"]["analysis"] / "{method}.done", method=config['variant_calling_methods']),
        expand(results_dict["annotated_vcfs"] / "{sample}.{method}.vcf", sample=SAMPLES, method=config['variant_calling_methods']),
        expand(results_dict["dotplots"] / "{sample}_dotplot.html", sample=SAMPLES),
        expand(results_dict["depth_histograms"] / "{sample}_depth_histogram.png", sample=SAMPLES),
        expand(results_dict["depth_genome"] / "{sample}_depth_genome.png", sample=SAMPLES),
        expand(results_dict["depth_genome_zscore"] / "{sample}_depth_genome_zscore.png", sample=SAMPLES),
        expand(results_dict["genome_bin_stats"] / "{sample}_genome_bin_stats.tsv", sample=SAMPLES),
        expand(results_dict["coverage_gc_correlation"] / "{sample}_coverage_gc_corr.png", sample=SAMPLES),
        expand(results_dict["distance_matix"] / "{method}_distance_matrix.tsv", method=config['variant_calling_methods']),
        expand(results_dict["distance_clustering"] / "{method}_distance_clustering.png", method=config['variant_calling_methods']),


if config["aligner"] == "hisat2":

    include: "rules/map_reads_hisat2.smk"


if config["aligner"] == "bwa":

    include: "rules/map_reads_bwa.smk"


if config["aligner"] == "bowtie2":

    include: "rules/map_reads_bowtie2.smk"

if config["aligner"] == "pacbio":

    include: "rules/map_reads_bwa_pacbio.smk"


include: "rules/run_trim.smk"


include: "rules/samtools.smk"


include: "rules/unzip.smk"


include: "rules/remove_PCR_duplicates.smk"


include: "rules/call_variants.smk"


include: "rules/post_analysis.smk"


include: "rules/align_genomes.smk"

if config["aligner"] != "pacbio":
    include: "rules/assembly_illumina.smk"

if config["aligner"] == "pacbio":
    include: "rules/assembly_pacbio.smk"